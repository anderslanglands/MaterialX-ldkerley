set(MATERIALX_DATA_LIBRARY_BUILD_DIR ${CMAKE_CURRENT_BINARY_DIR}/mxDataLibrary_build)

file(GLOB_RECURSE MTLX_DATA_LIBRARY_SOURCE_FILES
        RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
        CONFIGURE_DEPENDS
        LIST_DIRECTORIES false
        *.mtlx
        *.md
        *.glsl
        *.osl
        *.h
        *.metal)

foreach(SOURCE_FILE IN LISTS MTLX_DATA_LIBRARY_SOURCE_FILES)
    set(SOURCE_FILEPATH ${CMAKE_CURRENT_SOURCE_DIR}/${SOURCE_FILE})
    set(DEST_FILEPATH ${MATERIALX_DATA_LIBRARY_BUILD_DIR}/${SOURCE_FILE})
    add_custom_command(
            OUTPUT ${DEST_FILEPATH}
            COMMAND buildMaterialXLibraryTool ${SOURCE_FILEPATH} ${DEST_FILEPATH}
            DEPENDS ${SOURCE_FILEPATH}
    )
    list(APPEND MTLX_DATA_LIBRARY_BUILD_FILES ${DEST_FILEPATH})

endforeach ()

add_custom_target(buildMaterialXDataLibrary ALL
    DEPENDS ${MTLX_DATA_LIBRARY_BUILD_FILES})


set(MATERIALX_DATA_LIBRARY_BUILD_DIR ${MATERIALX_DATA_LIBRARY_BUILD_DIR} PARENT_SCOPE)
set(MTLX_DATA_LIBRARY_BUILD_FILES ${MTLX_DATA_LIBRARY_BUILD_FILES} PARENT_SCOPE)


set(MATERIALX_DATA_LIBRARY_STAGED_DIR ${CMAKE_CURRENT_BINARY_DIR}/mxDataLibrary_staged)

foreach(SOURCE_FILE IN LISTS MTLX_DATA_LIBRARY_SOURCE_FILES)

    set(SOURCE_FILEPATH ${MATERIALX_DATA_LIBRARY_BUILD_DIR}/${SOURCE_FILE})
    set(DEST_FILEPATH ${MATERIALX_DATA_LIBRARY_STAGED_DIR}/${SOURCE_FILE})

    get_filename_component(ext ${SOURCE_FILE} EXT)

    if(ext STREQUAL ".mtlx")
        add_custom_command(
                OUTPUT ${DEST_FILEPATH}
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${SOURCE_FILEPATH} ${DEST_FILEPATH}
                DEPENDS ${SOURCE_FILEPATH}
        )
        list(APPEND MTLX_DATA_LIBRARY_STAGED_FILES ${DEST_FILEPATH})
    endif()

endforeach ()


add_custom_target(stageMaterialXDataLibrary ALL
        DEPENDS ${MTLX_DATA_LIBRARY_STAGED_FILES})



set(MATERIALX_DATA_LIBRARY_STAGED_DIR ${MATERIALX_DATA_LIBRARY_STAGED_DIR} PARENT_SCOPE)
set(MTLX_DATA_LIBRARY_STAGED_FILES ${MTLX_DATA_LIBRARY_STAGED_FILES} PARENT_SCOPE)




if(NOT SKBUILD)
    install(DIRECTORY ${MATERIALX_DATA_LIBRARY_STAGED_DIR}/
            DESTINATION "${MATERIALX_INSTALL_STDLIB_PATH}"

    )
endif()

if(MATERIALX_BUILD_PYTHON)
    set(MATERIALX_PYTHON_LIBRARIES_PATH "${MATERIALX_PYTHON_FOLDER_NAME}/${MATERIALX_INSTALL_STDLIB_PATH}")
    if(SKBUILD)
        set(MATERIALX_PYTHON_LIBRARIES_PATH "${SKBUILD_PLATLIB_DIR}/MaterialX/libraries")
    endif()

    install(DIRECTORY ${MATERIALX_DATA_LIBRARY_STAGED_DIR}/
            DESTINATION "${MATERIALX_PYTHON_LIBRARIES_PATH}")
endif()
